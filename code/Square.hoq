import Filler
import Paths


data S1 = base | loop I
  with
    loop left  = base
    loop right = base

data Sq = a | b | c | d | bc I | ba I | cd I | ad I
  with
    bc left  = b
    bc right = c
    ba left  = b
    ba right = a
    cd left  = c
    cd right = d
    ad left  = a
    ad right = d

data El = s | t | st1 I | st2 I
  with
    st1 left  = s
    st1 right = t
    st2 left  = s
    st2 right = t

f1 : El -> S1
f1 s       = base
f1 (st1 _) = base
f1 (st2 j) = loop j
f1 t       = base

g1 : S1 -> El
g1 base     = t
g1 (loop j) = fill-2 (\_ _ -> El) (box-1 s t t t (path st1) idp (path st2)) right j

g1-then-f1 : (x : S1) -> x = f1 (g1 x)
g1-then-f1 base     = idp
g1-then-f1 (loop j) = path (\i -> f1 (fill-2 (\_ _ -> El) (box-1 s t t t (path st1) idp (path st2)) i j))

f1-then-g1 : (x : El) -> x = g1 (f1 x)
f1-then-g1 s       = path st1
f1-then-g1 (st1 j) = ptrim' (path st1) j
f1-then-g1 (st2 j) = path (\i -> (fill-2 (\_ _ -> El) (box-1 s t t t (path st1) idp (path st2)) i j))
f1-then-g1 t       = idp


f2 : Sq -> El
f2 a      = s
f2 (ba _) = s
f2 b      = s
f2 (bc j) = st1 j
f2 c      = t
f2 (cd _) = t
f2 d      = t
f2 (ad j) = st2 j

g2 : El -> Sq
g2 s       = a
g2 (st1 j) = fill-2 (\_ _ -> Sq) (box-1 b c a d (path ba) (path cd) (path bc)) right j
g2 (st2 j) = ad j
g2 t       = d

g2-then-f2 : (x : El) -> x = f2 (g2 x)
g2-then-f2 s       = idp
g2-then-f2 (st1 j) = path (\i -> f2 (fill-2 (\_ _ -> Sq) (box-1 b c a d (path ba) (path cd) (path bc)) i j))
g2-then-f2 (st2 _) = idp
g2-then-f2 t       = idp

f2-then-g2 : (x : Sq) -> x = g2 (f2 x)
f2-then-g2 a      = idp
f2-then-g2 (ba j) = ptrim' (path ba) j
f2-then-g2 b      = path ba
f2-then-g2 (bc j) = path (\i -> (fill-2 (\_ _ -> Sq) (box-1 b c a d (path ba) (path cd) (path bc)) i j))
f2-then-g2 c      = path cd
f2-then-g2 (cd j) = ptrim' (path cd) j
f2-then-g2 d      = idp
f2-then-g2 (ad _) = idp
